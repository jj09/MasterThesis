%!TEX root = JakubJedryszek-MasterThesis.tex

\cleardoublepage

\chapter{Rate controller thread from PCA pump AADL models}
\label{Appendix:AADL:RateController}

This appendix presents \lstinline{Rate_Controller} thread from \lstinline{PCA_Operation} module, from AADL/BLESS models of PCA pump, created by Brian Larson.

\singlespacing
\begin{lstlisting}[language=aadl, gobble=0, numbers=left, caption={\lstinline{Rate_Controller} thread}, label={listing:aadl:rate_controller_thread}]
thread Rate_Controller
features
    Infusion_Flow_Rate: out data port PCA_Types::Flow_Rate
      {BLESS::Assertion => "<<:=PUMP_RATE()>>";};   
    System_Status: out event data port PCA_Types::Status_Type;
    Begin_Infusion: in event port
      {BLESS::Assertion => "<<Rx_APPROVED()>>";};  
    Begin_Priming: in event port;
    End_Priming: in event port;
    Halt_Infusion: in event port;
    Square_Bolus_Rate: in data port PCA_Types::Flow_Rate 
      {BLESS::Assertion => "<<:=SQUARE_BOLUS_RATE>>";};
    Patient_Bolus_Rate: in data port PCA_Types::Flow_Rate 
      {BLESS::Assertion => "<<:=PATIENT_BOLUS_RATE>>";};
    Basal_Rate: in data port PCA_Types::Flow_Rate 
      {BLESS::Assertion => "<<:=BASAL_RATE>>";};
    VTBI: in data port PCA_Types::Drug_Volume 
      {BLESS::Assertion => "<<:=VTBI>>";};
    HW_Detected_Failure: in event port;
    Stop_Pump_Completely: in event port; 
    Pump_At_KVO_Rate: in event port; 
    Alarm : in event data port PCA_Types::Alarm_Type;
    Warning : in event data port PCA_Types::Warning_Type;
    Patient_Request_Not_Too_Soon: in event port 
      {BLESS::Assertion => "<<:=PATIENT_REQUEST_NOT_TOO_SOON(now)>>";};  
    Door_Open: in data port Base_Types::Boolean;
    Pause_Infusion: in event port;
    Resume_Infusion: in event port;
    CP_Clinician_Request_Bolus: in event port;
    CP_Bolus_Duration: in event data port ICE_Types::Minute; 
    Near_Max_Drug_Per_Hour: in event port  --near maximum drug infused in any hour
      {BLESS::Assertion => "<<PATIENT_NEAR_MAX_DRUG_PER_HOUR()>>";};  
    Over_Max_Drug_Per_Hour: in event port  --over maximum drug infused in any hour
      {BLESS::Assertion => "<<PATIENT_OVER_MAX_DRUG_PER_HOUR()>>";};  
    ICE_Stop_Pump: in event port;
  properties
    Thread_Properties::Dispatch_Protocol => Aperiodic;
end Rate_Controller;

thread implementation Rate_Controller.imp
annex BLESS
{**
assert
<<HALT : :(la=SafetyPumpStop) or (la=StopButton) or (la=EndPriming)>>  --pump at 0 if stop button, or safety architecture says, or done priming
<<KVO_RATE : :(la=KVOcommand) or (la=KVOalarm) or (la=TooMuchJuice)>>  --pump at KVO rate when commanded, some alarms, or excedded hourly limit
<<PB_RATE : :la=PatientButton>>  --patient button pressed, and allowed
<<CCB_RATE : :(la=StartSquareBolus) or (la=ResumeSquareBolus)>>  --clinician-commanded bolus start or resumption after patient bolus
<<PRIME_RATE : :la=StartPriming>>  --priming pump
<<BASAL_RATE : :(la=StartButton) or (la=ResumeBasal) or (la=SquareBolusDone)>>  --regular infusion
<<PUMP_RATE : :=
  (HALT()) -> 0,                                      --no flow
  (KVO_RATE()) -> PCA_Properties::KVO_Rate,           --KVO rate
  (PB_RATE()) -> Patient_Bolus_Rate,  --maximum infusion upon patient request
  (CCB_RATE()) -> Square_Bolus_Rate,                  --square bolus rate=VTBI/duration, from data port
  (PRIME_RATE()) -> PCA_Properties::Prime_Rate,       --pump priming
  (BASAL_RATE()) -> Basal_Rate                        --basal rate, from data port
>>
invariant <<true>>
variables
  --time of last action
  tla :BLESS_Types::Time := 0;
  la :   --last action
    enumeration ( 
      SafetyStopPump,   --safety architecture found a problem
      StopButton,       --clinician pressed stop button
      KVOcommand,       --from control panel (clinician) or ICE (app) to pump Keep-vein-open rate
      KVOalarm,         --some alarms should pump at KVO rate
      TooMuchJuice,     --exceeded max drug per hour, pump at KVO until prescription and patient are re-authenticated
      PatientButton,    --patient requested drug
      ResumeSquareBolus,--infusion of VTBI finished, resume clinician-commanded bolus
      ResumeBasal,      --infusion of VTBI finished, resume basal-rate
      StartSquareBolus, --begin clinician-commanded bolus
      SquareBolusDone,  --infusion of VTBI finished
      StartPriming,     --begin pump/line priming, pressed "prime" button
      EndPriming,       --end priming, pressed "prime" button again, or time-out 
      StartButton       --start pumping at basal rate
    );    
  pb_duration :BLESS_Types::Time  --patient button duration = VTBI/Patient_Bolus_Rate
    <<PB_DURATION : :pb_duration=(VTBI/Patient_Bolus_Rate)>>;
states
  PowerOn : initial state;        --power-on
  WaitForRx : complete state;   --wait for valid prescription
  CheckPBR : state  --check Patient_Bolus_Rate is positive
    <<Rx_APPROVED()>>;
  RxApproved : complete state   --prescription verified
    <<Rx_APPROVED() and PB_DURATION()>>;
  Priming : complete state      --priming the pump, 1 ml in 6 sec
    <<(la=StartPriming) and (Infusion_Flow_Rate@now = PCA_Properties::Prime_Rate) and PB_DURATION()>>;
  WaitForStart : complete state   --wait for clinician to press 'start' button
    <<HALT() and (Infusion_Flow_Rate@now=0) and PB_DURATION()>>;
  PumpBasalRate : complete state  --pumping at basal rate
    <<((la=StartButton) or (la=ResumeBasal)) and (Infusion_Flow_Rate@now=Basal_Rate@now) and PB_DURATION()>>;
  PumpPatientButtonVTBI : complete state  --pumping patient-requested bolus
    <<(la=PatientButton) and PB_DURATION()
      and (Infusion_Flow_Rate@now=Patient_Bolus_Rate)>>;
  PumpCCBRate : complete state    --pumping at clinician-commanded bolus rate
    <<((la=StartSquareBolus) or (la=ResumeSquareBolus)) and (Infusion_Flow_Rate@now=Square_Bolus_Rate@now) and PB_DURATION()>>;
  PumpKVORate : complete state    --pumping at keep-vein-open rate
    <<((la=KVOcommand) or (la=KVOalarm) or (la=TooMuchJuice))  and PB_DURATION()
      and (Infusion_Flow_Rate@now=PCA_Properties::KVO_Rate)>>;
  PumpingSuspended : complete state  --clinician pressed 'stop' button
    <<((la=StopButton) or (la=SafetyStopPump)) and (Infusion_Flow_Rate@now=0)>>;
  Crash : final state;    --abnormal termination
  Done : final state    --normal termination
    <<Infusion_Flow_Rate@now=0>>;
transitions
--wait for valid prescription
  go : PowerOn-[ true ]->WaitForRx{};  
--prescription validated
  rxo : WaitForRx-[ on dispatch Begin_Infusion ]-> CheckPBR{};
  pbr0 : CheckPBR-[ Patient_Bolus_Rate<=0 ]->Crash{}; --bad Patient_Bolus_Rate
  pbrok : CheckPBR-[ Patient_Bolus_Rate>0 ]->RxApproved
    {<<Rx_APPROVED() and (Patient_Bolus_Rate>0)>>  --likely will change from logic variable to predicate Rx_APPROVED()
      pb_duration := VTBI/Patient_Bolus_Rate  --calculate patient bolus duration
      --note division without knowing divsor is non-zero; should generate additional proof obligations for assignment using division
    <<Rx_APPROVED() and PB_DURATION()>>};  
--clinician press 'prime' button
  rxpri : RxApproved-[ on dispatch Begin_Priming ]-> Priming  
    {
    la :=StartPriming
        <<Begin_Priming@now and Rx_APPROVED() and (la = StartPriming) and PB_DURATION()>>
    ; 
    Infusion_Flow_Rate!(PCA_Properties::Prime_Rate)  --infuse at prime rate
        <<(la = StartPriming) and Rx_APPROVED() and PB_DURATION() and
          (Infusion_Flow_Rate@now=PCA_Properties::Prime_Rate)>>   
    };
--priming done, wait for start
  prd: Priming-[ on dispatch End_Priming or timeout (Begin_Priming) PCA_Properties::Prime_Time sec]-> WaitForStart  
    {
    la:=EndPriming
      <<HALT() and PB_DURATION()>>  --and Begin_Priming timed out
    ;
    Infusion_Flow_Rate!(0)   --stop priming flow
      <<HALT() and (Infusion_Flow_Rate@now=0) and PB_DURATION()>>
    };
--prime again
  pri: WaitForStart-[ on dispatch Begin_Priming ]-> Priming  
    {
    la:=StartPriming
        <<Begin_Priming@now and PB_DURATION() and PRIME_RATE()>>
    ; 
    Infusion_Flow_Rate!(PCA_Properties::Prime_Rate)  --infuse at prime rate
        <<PRIME_RATE() and PB_DURATION() and
          (Infusion_Flow_Rate@now=PCA_Properties::Prime_Rate)>>   
   };
--clinician press 'start' button after priming    
  sap: WaitForStart-[ on dispatch Begin_Infusion ]-> PumpBasalRate  --start after priming
    {
    la:=StartButton
      <<(la=StartButton) and Begin_Infusion@now  and PB_DURATION()>>    
    ;
    Infusion_Flow_Rate!(Basal_Rate)   --infuse at basal rate
      <<(la=StartButton) and (Infusion_Flow_Rate@now=Basal_Rate@now) and PB_DURATION()>>
    };
--Patient_Request_Bolus during basal rate infusion
  pump_basal_rate: 
  PumpBasalRate-[ on dispatch Patient_Request_Not_Too_Soon]-> PumpPatientButtonVTBI
    {
    la := PatientButton 
      <<(la=PatientButton) and Patient_Request_Bolus@now and PB_DURATION()>>    
    ;
    Infusion_Flow_Rate!(Patient_Bolus_Rate)   --infuse at patient button rate
      <<(la=PatientButton) and PB_DURATION()
        and (Infusion_Flow_Rate@now=Patient_Bolus_Rate)>>     
    };  --end of pump_basal_rate
--VTBI delivered
  vtbi_delivered: 
  PumpPatientButtonVTBI -[ on dispatch timeout (Infusion_Flow_Rate) pb_duration ms ]-> PumpBasalRate
    {
    la:=ResumeBasal
    ;
    <<(la=ResumeBasal) and PB_DURATION()>>  --and timeout of patient button duration
    Infusion_Flow_Rate!(Basal_Rate)   --infuse at basal rate
      <<(la=ResumeBasal) and (Infusion_Flow_Rate@now=Basal_Rate@now) and PB_DURATION()>>   
    };  --end of vtbi_delivered
**};
end Rate_Controller.imp;
\end{lstlisting} 
\doublespacing
